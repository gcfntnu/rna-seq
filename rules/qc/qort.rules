#-*- mode: snakemake -*-
"""QoRTs: Quality of RNA-seq Tool-Set
https://hartleys.github.io/QoRTs/
"""
QC_QORT = join(QC_INTERIM, 'qort')

rule qc_bam_qorts:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        bam = get_sorted_bam
    params:
        java_opt = '-Xms4G -Xmx4G',
        se = '--singleEnded' if len(config['read_geometry']) == 1 else '',
        max_len = '--maxReadLength {}'.format(int(config['read_geometry'][0])),
        stranded = '--stranded',
        read_strand = '--fr_secondStrand ' if LIBPREP['strand'] == 'forward' else '',
        outdir = lambda wildcards, output: os.path.dirname(output.log)
    output:
        log = join(QC_QORT, '{sample}', 'QC.summary.txt')
    threads:
        4
    singularity:
        'docker://' + config['docker']['qorts']
    shell:
        'qorts {params.java_opt} QC '
        '{params.se} '
        '{params.max_len} '
        '{params.stranded} '
        '{params.read_strand} '
        '{input.bam} '
        '{input.gtf} '
        '{params.outdir} '
