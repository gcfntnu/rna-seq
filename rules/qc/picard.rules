#-*- mode: snakemake -*-
"""Picard: A set of command line tools (in Java) for manipulating high-throughput sequencing (HTS) 
data and formats such as SAM/BAM/CRAM and VCF.

https://broadinstitute.github.io/picard/
"""

QC_PICARD = join(QC_INTERIM, 'picard')

rule qc_bam_create_ribointervals:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        bam = join(ALIGN_INTERIM, config.get('align', {}).get('method', 'star'), '{}.sorted.bam'.format(SAMPLES[0]))
    output:
        join(REF_DIR, 'genes', 'rrna.intervals')
    singularity:
        'docker://' + config['docker']['samtools']
    shell:
        """
        samtools view -H {input.bam} > /mnt/scratch/dummy
        grep rRNA {input.gtf} | awk '$3=="transcript"' | cut -f1,4,5,7,9 | perl -lane '/transcript_id "([^"]+)"/ or die "no transcript_id on $."; print join "\t", (@F[0,1,2,3], $1)' | sort -k1V -k2n -k3n >> /mnt/scratch/dummy
        mv /mnt/scratch/dummy {output}
        """

rule qc_bam_picard_rnametrics:
    input:
        bam = get_sorted_bam,
        ref_flat = join(REF_DIR, 'genes', 'genes.refflat.gz'),
        rrna = rules.qc_bam_create_ribointervals.output
    output:
        metrics = join(QC_PICARD, '{sample}.rnaseq.metrics')
    log:
        metrics = 'logs/{sample}/{sample}.rnaseq.metrics'
    params:
        java_opt='-Xms4g -Xmx4g'
    threads:
        4
    singularity:
        'docker://gcfntnu/picard:2.18'
    shell:
        """
        picard CollectRnaSeqMetrics {params.java_opt} INPUT={input.bam} OUTPUT={output.metrics} REF_FLAT={input.ref_flat} STRAND=SECOND_READ_TRANSCRIPTION_STRAND ASSUME_SORTED=TRUE RIBOSOMAL_INTERVALS={input.rrna} VALIDATION_STRINGENCY=SILENT
        cp {output.metrics} {log.metrics}
        """

rule qc_bam_picard_insertsize:
    input:
        bam = get_sorted_bam
    output:
        log = join(QC_PICARD, '{sample}.insert_size_metric.tsv'),
        pdf = join(QC_PICARD, '{sample}.insert_size_metric.pdf')
    log:
        'logs/{sample}/{sample}.insert_size_metric.tsv'
    singularity:
        'docker://gcfntnu/picard:2.18'
    shell:
        """
        picard CollectInsertSizeMetrics INPUT={input.bam} H={output.pdf} OUTPUT={output.log}
        cp {output.log} {log}
        """

