
rule qc_bam_create_ribointervals:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        bam = join(ALIGN_INTERIM, config.get('align', {}).get('method', 'star'), '{}.sorted.bam'.format(SAMPLES[0]))
    output:
        join(REF_DIR, 'genes', 'rrna.intervals')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        """
        samtools view -H {input.bam} > /mnt/scratch/dummy
        grep rRNA {input.gtf} | awk '$3=="transcript"' | cut -f1,4,5,7,9 | perl -lane '/transcript_id "([^"]+)"/ or die "no transcript_id on $."; print join "\t", (@F[0,1,2,3], $1)' | sort -k1V -k2n -k3n >> /mnt/scratch/dummy
        mv /mnt/scratch/dummy {output}
        """

rule qc_bam_picard_rnametrics:
    input:
        bam = get_sorted_bam,
        ref_flat = join(REF_DIR, 'genes', 'genes.refflat.gz'),
        rrna = rules.qc_bam_create_ribointervals.output
    output:
        metrics = 'logs/{sample}/collect_rnaseq.metrics'
    params:
        java_opt="-Xms2g -Xmx16g"
    threads:
        4
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'picard CollectRnaSeqMetrics '
        '{params.java_opt} '
        'INPUT={input.bam} '
        'OUTPUT={output.metrics} '
        'REF_FLAT={input.ref_flat} '
        'STRAND=SECOND_READ_TRANSCRIPTION_STRAND '
        'ASSUME_SORTED=FALSE '
        'RIBOSOMAL_INTERVALS={input.rrna} '
        'VALIDATION_STRINGENCY=SILENT '

rule qc_bam_picard_collect:
    input:
        bam = get_sorted_bam
    params:
        out = 'logs/{sample}'
    output:
        'logs/{sample}/insert_size_metric.tsv'
    log:
        'logs/{sample}/insert_size_metric.pdf'
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'picard CollectInsertSizeMetrics '
        'INPUT={input.bam} '
        'H={log} '
        'OUTPUT={output} '

rule qc_bam_rseqc_read_distribution:
    input:
        bam = get_sorted_bam,
        bed = join(REF_DIR, 'genes', 'genes.bed12')
    singularity:
        'docker://quay.io/biocontainers/rseqc:3.0.0--py36hf8a1672_0'
    output:
        'logs/{sample}/rseqc.readdist.txt'
    shell:
        'read_distribution.py  -i {input.bam} -r {input.bed} > {output}'

rule qc_bam_rseqc_junction_annotation:
    input:
        bam = get_sorted_bam,
        bed = join(REF_DIR, 'genes', 'genes.bed12')
    singularity:
        'docker://quay.io/biocontainers/rseqc:3.0.0--py36hf8a1672_0'
    params:
        output = 'logs/{sample}/rseqc'
    shadow:
        'shallow'
    output:
        'logs/{sample}/rseqc.junction.xls'
    shell:
        'junction_annotation.py  -i {input.bam} -r {input.bed} -o {params.output}'
        
rule qc_bam_rseqc_junction_saturation:
    input:
        bam = get_sorted_bam,
        bed = join(REF_DIR, 'genes', 'genes.bed12')
    singularity:
        'docker://quay.io/biocontainers/rseqc:3.0.0--py36hf8a1672_0'
    params:
        output = 'logs/{sample}/rseqc'
    shadow:
        'shallow'
    output:
        'logs/{sample}/rseqc.junctionSaturation_plot.r'
    shell:
        'junction_saturation.py  -i {input.bam} -r {input.bed} -o {params.output}'


rule qc_bam:
    input:
       expand( 'logs/{sample}/collect_rnaseq.metrics', sample=SAMPLES)
        

