#-*- mode: snakemake -*-
"""Picard: A set of command line tools (in Java) for manipulating high-throughput sequencing (HTS) 
data and formats such as SAM/BAM/CRAM and VCF.

https://broadinstitute.github.io/picard/
"""
rule qc_bam_create_ribointervals:
    input:
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        bam = join(ALIGN_INTERIM, config.get('align', {}).get('method', 'star'), '{}.sorted.bam'.format(SAMPLES[0]))
    output:
        join(REF_DIR, 'genes', 'rrna.intervals')
    singularity:
        'docker://flatberg/refdb:0.1'
    shell:
        """
        samtools view -H {input.bam} > /mnt/scratch/dummy
        grep rRNA {input.gtf} | awk '$3=="transcript"' | cut -f1,4,5,7,9 | perl -lane '/transcript_id "([^"]+)"/ or die "no transcript_id on $."; print join "\t", (@F[0,1,2,3], $1)' | sort -k1V -k2n -k3n >> /mnt/scratch/dummy
        mv /mnt/scratch/dummy {output}
        """

rule qc_bam_picard_rnametrics:
    input:
        bam = get_sorted_bam,
        ref_flat = join(REF_DIR, 'genes', 'genes.refflat.gz'),
        rrna = rules.qc_bam_create_ribointervals.output
    output:
        metrics = 'logs/{sample}/collect_rnaseq.metrics'
    params:
        java_opt="-Xms2g -Xmx16g"
    threads:
        4
    singularity:
        'docker://flatberg/refdb:0.1'
    shell:
        'picard CollectRnaSeqMetrics '
        '{params.java_opt} '
        'INPUT={input.bam} '
        'OUTPUT={output.metrics} '
        'REF_FLAT={input.ref_flat} '
        'STRAND=SECOND_READ_TRANSCRIPTION_STRAND '
        'ASSUME_SORTED=FALSE '
        'RIBOSOMAL_INTERVALS={input.rrna} '
        'VALIDATION_STRINGENCY=SILENT '

rule qc_bam_picard_insertsize:
    input:
        bam = get_sorted_bam
    params:
        out = 'logs/{sample}'
    output:
        'logs/{sample}/insert_size_metric.tsv'
    log:
        'logs/{sample}/insert_size_metric.pdf'
    singularity:
        'docker://flatberg/refdb:0.1'
    shell:
        'picard CollectInsertSizeMetrics '
        'INPUT={input.bam} '
        'H={log} '
        'OUTPUT={output} '

