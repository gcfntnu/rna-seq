#-*- mode: snakemake -*-
"""Snakemake rules for alignment free transcript and gene expression modelling using salmon.


Dependencies
------------
SALMON, https://combine-lab.github.io/salmon/

Install:
conda install salmon

"""

from os.path import join

extra_conf_fn = srcdir('salmon.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh) or {}
        update_config2(config, c)
        
SALMON_INTERIM = join(QUANT_INTERIM, 'salmon')
SALMON_OUT = join(QUANT_PROCESSED, 'salmon')

REF_DIR = locals()[config['db']['reference'].upper() + '_EXT']

## config params
read_orientation = config.get('read_orientation')
if read_orientation is None:
    LIB_TYPE = 'A'
else:
    read_orientation = read_orientation.lower()
    stranded = 'U' if read_orientation == 'u' else 'S'
    orientation = 'R' if read_orientation == 'rf' else 'F' if read_orientation == 'fr' else ''
    LIB_TYPE = 'I' + stranded + orientation

def salmon_r1(wildcards):
    """Build input args for multi fastq input
    """
    R1 = get_processed_fastq_R1(wildcards)
    return ' '.join(R1)

def salmon_r2(wildcards):
    """Build input args for multi fastq input
    """
    R2 = get_processed_fastq_R2(wildcards)
    return ' '.join(R2)

def get_fragment_length(wildcards):
    val = config['samples'][wildcards.sample].get('Fragment_Length', 180)
    if val == 'NA':
        val = 180
    return val
    
def get_fragment_sd(wildcards):
    val = config['samples'][wildcards.sample].get('Fragment_SD', 20)
    if val == 'NA':
        val = 20
    return val
    
def sample_args(wildcards):
    """Sample specific args.
    """
    args = ''
    sample = config['samples'][wildcards.sample]
    PE = sample.get('paired_end')
    if PE is None:
        PE = sample.get('R2') is not None
    else:
        PE = bool(PE)
    if PE:
        args += ' -1 {} -2 {} '.format(salmon_r1(wildcards), salmon_r2(wildcards))
        args += '--gcBias '
    else:
        args += ' -r {} '.format(salmon_r1(wildcards))
        args += '--fldMean {} '.format(get_fragment_length(wildcards))
        args += '--fldSD {} '.format(get_fragment_sd(wildcards))
    return args

rule salmon_transcriptome_index:
    input:
        transcriptome = join(REF_DIR, 'fasta', 'transcriptome.fa')
    params:
        args = '--perfectHash -k 31 ',
        index_dir = join(REF_DIR, 'salmon')
    output:
        join(REF_DIR, 'salmon', 'refInfo.json')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    threads:
        24
    shell:
        'salmon index '
        '{params.args} '
        '-i {params.index_dir} '
        '--transcripts  {input.transcriptome} '
        '--threads {threads} '

rule salmon_map:
    input:
        R1 = get_processed_fastq_R1,
        index = join(REF_DIR, 'salmon', 'refInfo.json'),
        gtf = join(REF_DIR, 'genes', 'genes.gtf')
    params:
        output = join(SALMON_INTERIM, '{sample}'),
        nboot = 30,
        lib_type = LIB_TYPE,
        index = rules.salmon_transcriptome_index.params.index_dir,
        sample_args = sample_args
    threads:
        24
    conda:
        'envs/quant.yaml'
    output:
        quant = join(SALMON_INTERIM, '{sample}', 'quant.sf'),
        log = join(SALMON_INTERIM, '{sample}', 'aux_info', 'meta_info.json')
    log:
        'logs/{sample}/meta_info.json'
    shell:
        'salmon quant '
        '-i {params.index} '
        '-l {params.lib_type} '
        '-g {input.gtf} '
        '-p {threads} '
        '--numBootstraps {params.nboot} '
        '--writeUnmappedNames '
        '--validateMappings '
        '--seqBias '
        '-o {params.output} '
        '{params.sample_args} '
        '&& cp {output.log}  {log}'

rule salmon_quant:
    input:
        files = expand(rules.salmon_map.output.quant, sample=SAMPLES),
        txinfo = join(REF_DIR, 'genes', 'transcripts.csv'),
        ginfo = join(REF_DIR, 'genes', 'genes.csv')
    params:
        output = SALMON_OUT,
        script = srcdir('scripts/transcript_quant.R')
    output:
        gene_counts = join(SALMON_OUT, 'genes.quant'),
        gene_tpm = join(SALMON_OUT, 'genes.tpm'),
        gene_info = join(SALMON_OUT, 'gene_info.tsv'),
        gene_length = join(SALMON_OUT, 'genes.length'),
        transcript_counts = join(SALMON_OUT, 'transcripts.quant'),
        transcript_tpm = join(SALMON_OUT, 'transcripts.tpm'),
        transcript_info = join(SALMON_OUT, 'transcript_info.tsv'),
        transcript_length = join(SALMON_OUT, 'transcripts.length')
    conda:
        'envs/quant.yaml'
    shell:
        'Rscript {params.script} '
        '--type salmon '
        '--output-length-scaled-tpm '
        '--output-genelength '
        '--output-transcripts '
        '--txinfo {input.txinfo} '
        '--ginfo {input.ginfo} '
        '-o {params.output} '
        ' {input.files} '
