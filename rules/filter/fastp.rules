#-*- mode:snakemake -*-

rule fastp_interleave_fastq:
    input:
        R1 = _filter_get_raw_R1,
        R2 = _filter_get_raw_R2,
    output:
        pipe(join(FILTER_INTERIM, 'interleaved_fastq', '{sample}.fastq'))
    params:
        script = srcdir('scripts/interleave_fastq.sh')
    shell:
        '{params.script} <(zcat {input.R1}) <(zcat {input.R2}) > {output}'

rule fastp_se_join:
    input:
        _filter_get_raw_R1
    output:
        join(FILTER_INTERIM, 'concat', 'fastp_se', '{sample}_R1.fastq.gz')
    params:
        config['filter']['fastp']['params']
    log:
        json = 'logs/{sample}/fastp.json',
        html = 'logs/{sample}/fastp.html'
    singularity:
        config['filter']['docker']
    shell:
        ' zcat {input} | fastp --stdin -o {output.R1} -j {log.json} -h {log.html} {params}'

rule fastp_pe_join:
    input:
        join(FILTER_INTERIM, 'interleaved_fastq', '{sample}.fastq')
    output:
        R1 = join(FILTER_INTERIM, 'concat', 'fastp_pe', '{sample}_R1.fastq.gz'),
        R2 = join(FILTER_INTERIM, 'concat', 'fastp_pe', '{sample}_R2.fastq.gz')
    threads:
        3
    singularity:
        config['filter']['docker']
    params:
        config['filter']['fastp']['params']
    log:
        json = 'logs/{sample}/fastp.json',
        html = 'logs/{sample}/fastp.html'        
    shell:
        'cat {input} | fastp --stdin --interleaved_in -o {output.R1} -O {output.R2} -j {log.json} -h {log.html} {params}'

rule fastp_se:
    input:
        R1 = join(FASTQ_DIR, '{sample}_R1.fastq.gz')
    output:
        R1 = join(FILTER_INTERIM, 'fastp_se', '{sample}_R1.fastq.gz')
    params:
        config['filter']['fastp']['params']
    log:
        json = 'logs/{sample}/fastp.json',
        html = 'logs/{sample}/fastp.html'
    singularity:
        config['filter']['docker']
    shell:
        'fastp -i {input.R1} -o {output.R1} -j {log.json} -h {log.html} {params}'

rule fastp_pe:
    input:
        R1 = join(FASTQ_DIR, '{sample}_R1_001.fastq.gz'),
        R2 = join(FASTQ_DIR, '{sample}_R2_001.fastq.gz')
    output:
        R1 = join(FILTER_INTERIM, 'fastp_pe', '{sample}_R1_001.fastq.gz'),
        R2 = join(FILTER_INTERIM, 'fastp_pe', '{sample}_R2_001.fastq.gz')
    threads:
        3
    singularity:
        config['filter']['docker']
    params:
        config['filter']['fastp']['params']
    log:
        json = 'logs/{sample}/fastp.json',
        html = 'logs/{sample}/fastp.html'        
    shell:
        'fastp -i {input.R1} -I {input.R2} -o {output.R1} -O {output.R2} -j {log.json} -h {log.html} {params}'
