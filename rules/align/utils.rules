#-*- mode: snakemake -*-

"""
Picard tools, https://broadinstitute.github.io/picard/

"""

def optical_dup_args(*args, **kw):
    args = 'TAGGING_POLICY=All '
    machine = config.get('machine', 'nextseq500')
    if machine == 'nextseq500':
        args += 'OPTICAL_DUPLICATE_PIXEL_DISTANCE=100 '
    else:
        args += 'OPTICAL_DUPLICATE_PIXEL_DISTANCE=2500 '
    if config.get('remove_optical_duplicates'):
        args += 'REMOVE_SEQUENCING_DUPLICATES=TRUE '
    return args

rule picard_mark_duplicates:
    input:
        bam = join(ALIGN_INTERIM, ALIGNER, '{sample}.Aligned.sortedByCoord.out.bam'),
        mem = '.align.finalized'
    output:
        bam = join(ALIGN_INTERIM, ALIGNER, '{sample}.sorted.bam'),
        bai = join(ALIGN_INTERIM, ALIGNER, '{sample}.sorted.bai'),
        bai2 = join(ALIGN_INTERIM, ALIGNER, '{sample}.sorted.bam.bai'),
        metrics = 'logs/{sample}/{sample}.rmdup.metrics'
    log:
        'logs/{sample}/{sample}.picard.rmdup.log'
    params:
        java_opt="-Xms4g -Xmx4g ",
        dup_args = optical_dup_args()
    threads:
        8
    singularity:
        'docker://gcfntnu/picard:2.18'
    shell:
        'picard MarkDuplicates '
        '{params.java_opt} '
        'INPUT={input.bam} '
        'OUTPUT={output.bam} '
        'METRICS_FILE={output.metrics} '
        'VALIDATION_STRINGENCY=SILENT '
        'MAX_FILE_HANDLES_FOR_READ_ENDS_MAP=1000 '
        'CREATE_INDEX=TRUE '
        'CREATE_MD5_FILE=TRUE '
        '{params.dup_args}'
        '&& ln -rs {output.bai} {output.bai2} '
        
rule bam_namesort:
    input:
       bam = join(ALIGN_INTERIM, '{aligner}', '{sample}.sorted.bam')
    output:
        temp(join(ALIGN_INTERIM, '{aligner}', '{sample}.namesorted.bam'))
    threads:
        8
    singularity:
        'docker://quay.io/biocontainers/sambamba:0.7.0--h89e63da_0'
    shell:
        'sambamba sort -N -p -m 24G -t 8 -o {output} {input.bam}'
